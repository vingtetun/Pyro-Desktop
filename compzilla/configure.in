
AC_INIT(compzilla, 0.1, alex@beatniksoftware.com)

AC_CONFIG_SRCDIR(compzilla/install.rdf)
AC_CONFIG_HEADERS(config.h)

AM_INIT_AUTOMAKE

AM_MAINTAINER_MODE
AM_DISABLE_STATIC
AM_PROG_LIBTOOL
AC_PROG_CC
AC_PROG_INSTALL


PKG_CHECK_MODULES(GDK, gdk-2.0 >= 2.8.0)

PKG_CHECK_MODULES(NSPR, firefox-nspr >= 1.5.0)

FIREFOX_XPCOM="firefox-xpcom >= 1.5.0"

GECKO_IDLDIR=`$PKG_CONFIG --variable=idldir "$FIREFOX_XPCOM"`
GECKO_LIBDIR=`$PKG_CONFIG --variable=libdir "$FIREFOX_XPCOM"`
GECKO_INCLUDEDIR=`$PKG_CONFIG --variable=includedir "$FIREFOX_XPCOM"`

XPIDL="$GECKO_LIBDIR"/xpidl
if ! test -f "$XPIDL"; then
   AC_MSG_ERROR([You need to have xpidl])
fi

AC_SUBST(GECKO_IDLDIR)
AC_SUBST(GECKO_LIBDIR)
AC_SUBST(GECKO_INCLUDEDIR)
AC_SUBST(XPIDL)


AC_CACHE_CHECK(if Gecko was compiled to use Thebes, ac_cv_gecko_thebes, [
	CPPFLAGS_save="$CPPFLAGS"
	CPPFLAGS="$CPPFLAGS -I${GECKO_INCLUDEDIR}"
	AC_EGREP_CPP(yes, [
		#include "mozilla-config.h"
		#ifdef MOZ_CAIRO_GFX
		yes
		#endif
	], ac_cv_gecko_thebes=yes, ac_cv_gecko_thebes=no)
	CPPFLAGS="$CPPFLAGS_save"
])

AM_CONDITIONAL(GECKO_USE_THEBES, test "x$ac_cv_gecko_thebes" = "xyes")

##
## Checks for our Xextensions
##
#PKG_CHECK_MODULES(XEXTENSIONS, xcomposite xdamage xevie)
PKG_CHECK_MODULES(XEXTENSIONS, xcomposite xdamage)


AC_OUTPUT([
Makefile
compzilla/Makefile
])


dnl ==========================================================================
echo "

        Compzilla $VERSION
	==================

        prefix:                   ${prefix}
        compiler:                 ${CC}
	xpidl:			  ${XPIDL}
	Gecko includes:		  ${GECKO_INCLUDEDIR}
	Gecko idl files:	  ${GECKO_IDLDIR}
	Gecko using thebes:       ${ac_cv_gecko_thebes}
        XEXTENSIONS_CFLAGS:       ${XEXTENSIONS_CFLAGS}
        XEXTENSIONS_LIBS:         ${XEXTENSIONS_LIBS}
        GDK_CFLAGS:               ${GDK_CFLAGS}

        Now type 'make' to build $PACKAGE
"
